plugins {
    id 'java'
    id 'io.qameta.allure' version '2.11.2'
}

allure {
    autoconfigure = true
    version = '2.29.0'
    reportDir = file("build/allure-report")
}

wrapper {
    gradleVersion = '8.10.2'
}

repositories {
    mavenCentral()
}

group = 'com.example.testng'
version = '1.0-SNAPSHOT'

def allureVersion = '2.29.0'
def aspectJVersion = '1.9.22'
def kotlinVersion = '1.9.25'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.compilerArgs.add('-parameters')
}

configurations {
    agent {
        canBeResolved = true
        canBeConsumed = true
    }
}

dependencies {
    agent "org.aspectj:aspectjweaver:$aspectJVersion"

    testImplementation "org.testng:testng:7.9.0"
    
    testImplementation platform("io.qameta.allure:allure-bom:$allureVersion")
    testImplementation "io.qameta.allure:allure-testng:2.29.0"

    testImplementation "org.slf4j:slf4j-simple:2.0.16"
}

test {
    ignoreFailures = true
    useTestNG()
    jvmArgs = [ "-javaagent:${configurations.agent.singleFile}" ]
}



